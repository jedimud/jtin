#nop ----- buffs -----

#action {^The detect invisible wears off.$} {auto_buff_lost {DETECT INVISIBILITY}}
#action {^The aura around your body fades.$} {auto_buff_lost {SANCTUARY}}
#action {^Your infravision fades.$} {auto_buff_lost {INFRAVISION}}
#action {^You feel less righteous.$} {auto_buff_lost {BLESS}}
#action {^You feel less prepared for combat.$} {auto_buff_lost {AID}}
#action {^Your skin softens.$} {auto_buff_lost {STONE SKIN}}
#action {^You feel less protected from the rigors of the world.$} {auto_buff_lost {ARMOR}}
#action {^You feel less protected from good.$} {auto_buff_lost {PROTECTION FROM GOOD}}
#action {^You feel less protected from evil.$} {auto_buff_lost {PROTECTION FROM EVIL}}

#nop ----- debuffs -----



#nop ----- utility -----

#var print_method console;
#var console_id {};
#var console_src {}

#alias auto_buff_lost {
    auto_buff_print_msg_warn {>> LOST %1 <<}
}

#alias auto_buff_print_msg_info {
    #if {"$print_method" == "console"} {
        auto_buff_print_console_info {%1};
    };
    #elseif {"$print_method" == "group"} {
        auto_buff_print_group_info {%1};
    }
}

#alias auto_buff_print_msg_warn {
    #if {"$print_method" == "console"} {
        auto_buff_print_console_warn {%1};
    };
    #elseif {"$print_method" == "group"} {
        auto_buff_print_group_warn {%1};
    }
}

#alias auto_buff_print_console_info {
    auto_buff_print_console {<afc>%1};
}

#alias auto_buff_print_console_warn {
    auto_buff_print_console {<faa>%1};
}

#alias auto_buff_print_console {
    #if {"$console_id" != ""} {
        $console_id #showme <dae>[AUTO-BUFF] $console_src%1;
    } {
        #showme <dae>[AUTO-BUFF] %1;
    };
}

#alias auto_buff_print_group_info {
    gsay @M[AUTO-BUFF] @C%1;
}

#alias auto_buff_print_group_warn {
    gsay @M[AUTO-BUFF] @R%1;
}

#alias auto_buff { 
    #if {"%1" == "set"} {
        #if {"%2" == "console"} {
            auto_buff_print_msg_info {setting console output to: %3};
            #var console_id {%3};
        };
        #elseif {"%2" == "source"} {
            auto_buff_print_msg_info {setting source output to: %3};
            #var console_src {<afc>%3 };
        };
        #else {
            auto_buff_print_msg_info WHAT?!;
        }
    };
    #elseif {"%1" == "toggle"} {
        #if {"%2" == "group"} {
            #var print_method group;
            auto_buff_print_msg_info {toggling ouput to gsay};
        };
        #elseif {"%2" == "console"} {
            #var print_method console;
            auto_buff_print_msg_info {toggling output to console};
        };
        #else {
            auto_buff_print_msg_info WHAT?!;
        }
    };
    #else {
        auto_buff_print_msg_info WHAT?!;
    }
}