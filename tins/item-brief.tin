#sub { (invisible)} {}
#sub { (glowing)} {}
#sub { (humming)} {}

#action {^<offhand wield>} {#line gag}

#alias {item_brief} {
    #if {"%1" == "toggle"} {
        #if {"%2" == "insured"} {
            #if {"$item_brief_insured_tag" == "true"} {
                #var {item_brief_insured_tag} {false};
                print_item_brief_msg {toggling off (insured) tag}
            } {
                #var {item_brief_insured_tag} {true};
                print_item_brief_msg {toggling on (insured) tag}
            };
            item_brief_load_actions;
        };

        #elseif {"%2" == "sac"} {
            #if {"$item_brief_sac" == "true"} {
                #var {item_brief_sac} {false};
                print_item_brief_msg {toggling off sac briefs};
            } {
                #var {item_brief_sac} {true};
                print_item_brief_msg {toggling on sac briefs};
            };
            item_brief_load_actions;
        };
        
        #else {
            print_item_brief_msg {what?! Try: item_brief toggle [insured | sac ]};
        };
    } {
        print_item_brief_msg {what?! Try: item_brief toggle [ insured | sac ]};
    };
}

#alias print_item_brief_msg {
    #line ignore #showme <afc>[ITEM-BRIEF] %1;
    #line ignore #showme { };
}

#action {^You are carrying:} {
    item_brief_markup
}

#action {^%1 (carried):} {
    item_brief_markup
}

#alias item_brief_markup {
    #class item-brief-action read tins/item-brief-action.tin;
    #delay {0.5} {
        end_item_brief;
    };
}

#alias end_item_brief {
    #class item-brief-action kill;
    #class item-brief-action clear;
}

#alias item_brief_load_actions {
    #if {"$item_brief_insured_tag" == "true"} {
        #class item-brief-equip clear;
        #class item-brief-insured read tins/item-brief-insured.tin;
    } {
        #class item-brief-insured clear;
        #class item-brief-equip read tins/item-brief-equip.tin;
    }
}

#alias item_brief_find {
    #var item_name {"%1"};
    #replace item_name {'} {_};
    #script {item} {sh -c './sh/item-brief_find.sh $item_name'};
}

#nop ----- default on load -----

#var {item_brief_insured_tag} {true}
#var {item_brief_sac} {false}
item_brief_load_actions

