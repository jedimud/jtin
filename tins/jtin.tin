#alias jt_print_message {
    #showme <aef>jt :: %1;
}

#alias jt_print_message_with_buffer {
    jt_print_message {%1};
    #showme ;
}

#alias jt_print_state {
    #showme ;
    #showme <aef>[jt] JediMUD TinTin++;
    #showme ;
    jt_m_print_state;
    #showme ;
    jt_ib_print_state;
    #showme ;
    jt_ab_print_state;
    #showme ;
    jt_dl_print_state;
    #showme ;
    jt_at_print_state;
    #showme ;
    #cr;
}

#alias jt {
    #if {"%1" == "state"} {
        jt_print_state;
    };
    #elseif {"%1" == "map"} {
        jt_m {%2};
    };
    #elseif {"%1" == "item-brief"} {
        jt_ib {%2};
    };
    #else {
        jt_print_message_with_buffer {<efc>unknown command, try :: jt (state|map|item-brief)};
    }
}


#nop ----- auto-tank -----


#class jtin-auto-tank clear;
#class jtin-auto-tank read tins/jtin-auto-tank.tin;


#nop ----- data-logs -----


#class jtin-data-logs kill;
#class jtin-data-logs read tins/jtin-data-logs.tin;


#nop ----- item-brief -----

#alias jt_ib {
    #if {"%1" == "disabled"} {
        jt_ib_unload;
    };
    #elseif {"%1" == "stats"} {
        jt_ib_load %1;
        jt_ib_load_stats;
    };
    #elseif {"%1" == "sac"} {
        jt_ib_load %1;
    };
    #elseif {"%1" == "insured"} {
        jt_ib_load %1;
        jt_ib_load_insured;
    };
    #else {
        jt_ib_print_msg {<efc>unknown command, try :: jt item-brief (stats|insured|sac|disabled)};
    }
}

#alias jt_ib_default {
    #var jt_ib_state disabled;
}

#alias jt_ib_unload {
    jt_ib_print_msg {<fca>disabled};
    #class jtin-item-brief kill;
    #class jtin-item-brief-equip kill;
    #class jtin-item-brief-insured kill;
    #var jt_ib_state disabled;
}

#alias jt_ib_load {
    #class jtin-item-brief kill;
    #class jtin-item-brief read tins/jtin-item-brief.tin;
    #var jt_ib_state %1;
    jt_ib_print_msg {<afc>$jt_ib_state};
}

#alias jt_ib_load_insured {
    #class jtin-item-brief-equip kill;
    #class jtin-item-brief-insured read tins/jtin-item-brief-insured.tin;
}

#alias jt_ib_load_stats {
    #class jtin-item-brief-insured kill;
    #class jtin-item-brief-equip read tins/jtin-item-brief-equip.tin;
}

#alias jt_ib_print_state {
    #if {"$jt_ib_state" == "disabled"} {
        #showme <fac>* <adf>ITEM-BRIEF: <fca>$jt_ib_state;
    } {
        #showme <fac>* <adf>ITEM-BRIEF: <afc>$jt_ib_state;
    }
}

#alias jt_ib_print_msg {
    jt_print_message_with_buffer {item-brief :: %1}
}

#nop ----- mapper -----


#alias jt_m {
    #if {"%1" == "enabled"} {
        jt_m_load;
    };
    #elseif {"%1" == "disabled"} {
        jt_m_unload;
    };
    #else {
        jt_m_print_msg {<efc>unknown command, try :: jt map (enabled|disabled)};
    }
}

#alias jt_m_default {
    #var jt_m_state disabled;
}

#alias jt_m_unload {
    jt_m_hide;
    jt_m_print_msg {<fca>disabled};
    #class jtin-mapper kill;
    #var jt_m_state disabled;
}

#alias jt_m_load {
    #class jtin-mapper kill;
    #class jtin-mapper read tins/jtin-mapper.tin;
    #var jt_m_state enabled;
    jt_m_print_msg {<afc>enabled};
}

#alias jt_m_print_state {
    #if {"$jt_m_state" == "enabled"} {
        #showme <fac>* <adf>MAP: <afc>$jt_m_state;
    } {
        #showme <fac>* <adf>MAP: <fca>$jt_m_state;
    }
}

#alias jt_m_print_msg {
    jt_print_message_with_buffer {map :: %1}
}


#nop ----- auto-buff -----


#class jtin-auto-buff kill;
#class jtin-auto-buff read tins/jtin-auto-buff.tin;


#nop ----- startup -----

jt_m_default;
jt_ib_default;